{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

() recv_internal() {
}

global tuple head;
global builder current_element;

forall X -> int is_null (X x) asm "ISNULL";
int bit_count(int x) asm "UBITSIZE";

;; I'm thinking that full value of the value must be loaded
() load_to_list(int value) impure inline {

  int size = bit_count(value) - 1;
  value &= ~ (1 << size);

  do {
    int to_load = min(1023 - current_element.builder_bits(), size);
    current_element~store_uint(value >> (size - to_load), to_load);
    size -= to_load;
    value &= ~ (-1 << size);

    if(current_element.builder_bits() == 1023) {
      head = cons(current_element, head);
      current_element = begin_cell();
    }
  } until(size == 0);

  return ();
}

(int, slice) load_bits_from(int to_compare, slice list_slice) inline {

  while((bit_count(to_compare) < 256) & (~ list_slice.null?())) {
    int to_load = min(256 - bit_count(to_compare), list_slice.slice_bits());
    to_compare = (to_compare << to_load) | list_slice~load_uint(to_load);

    if(list_slice.slice_bits() == 0) {
      if(list_slice.slice_refs()) {
        list_slice = (list_slice~load_ref()).begin_parse();
      } else {
        return (to_compare, null());
      }
    }
  }

  return (to_compare, list_slice);
}

;; Building cell linked-list from the list-style list
cell build_list() inline {
  cell result = current_element.end_cell();

  while(~ head.is_null()) {
    (current_element, head) = uncons(head);
    result = current_element.store_ref(result).end_cell();
  }

  return result;
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {

  slice list_slice = linked_list.begin_parse();

  if(list_slice.slice_empty?()) {
    return begin_cell().end_cell();
  }

  int flag_size = bit_count(flag);
  int flag_bit_mask = ~ (-1 << flag_size);
  int to_compare = 1;
  tuple head = null();
  builder current_element = begin_cell();
  value = value | (1 << bit_count(value));

  do {
    (to_compare, list_slice) = load_bits_from(to_compare, list_slice);
    int bit_iterations = bit_count(to_compare) - 1 - flag_size;

    while(bit_iterations >= 0) {
      if(((to_compare >> bit_iterations) & flag_bit_mask) == flag) {
        load_to_list(to_compare >> (flag_size + bit_iterations));
        load_to_list(value);
        to_compare = (to_compare & (~ (-1 << bit_iterations))) | (1 << bit_iterations);
        bit_iterations -= flag_size;
      } else {
        bit_iterations -= 1;

        if(bit_iterations < 0) {
          load_to_list(to_compare);
          to_compare = 1;
        }
      }
    }

  } until(list_slice.null?());

  if(bit_count(to_compare) > 1) {
    load_to_list(to_compare);
  }

  return build_list();
}
